T5 Laboratory Simulation Script

.param R1a_value=1k
.param R1b_value=10k
.param R2_value=1k
*1 resistencia em serie com 2 em paralelo
.param R3_value=150k
.param R4a_value=1k
.param R4b_value=10k
.param C1_value=220n
*2 condensadores em serie
.param C2_value=110n


.csparam R1a_value={R1a_value}
.csparam R1b_value={R1b_value}
.csparam R2_value={R2_value}
.csparam R3_value={R3_value}
.csparam R4a_value={R4a_value}
.csparam R4b_value={R4b_value}
.csparam C1_value={C1_value}
.csparam C2_value={C2_value}


*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 10.0
Vee vee 0 -10.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 1 2 vcc vee 3 uA741


C1 in 1 {C1_value}

R1a 1 0 {R1a_value}
R1b 1 0 {R1b_value}
R3 2 3 {R3_value}
R4a 2 0 {R4a_value}
R4b 2 0 {R4b_value}


R2 3 out {R2_value}
C2 out 0 {C2_value}


.op
.end

.control

op

print all


ac dec 10 10 100MEG

plot vdb(out)
plot vp(out)*180/PI
hardcopy vo1f.ps vdb(out) 
hardcopy vo2f.ps vp(out)*180/PI


*gain+phasegain

let gain = db(v(out)/v(in))
plot gain
hardcopy gain.ps gain
echo gain_FIG


let gainphase = ph(v(out)/v(in))*180/pi
plot gainphase
hardcopy phase.ps gainphase
echo phase_FIG

meas AC wanted MAX vdb(out)
let ref=wanted-3


meas AC low WHEN vdb(out) = ref
meas AC up WHEN vdb(out) = ref CROSS=LAST

let Cfreq = sqrt(low*up)


let gainfreq = abs(v(out)[20]/v(in)[20])
let gaindb = db(v(out)[20]/v(in)[20])

let resist=(R1a_value+R1b_value+R2_value+2*R3_value+R4a_value+R4b_value)*0.001
let capac=(C1_value+4*C2_value)*1000000
let opampcost=1.3323e+04
let cost=resist+capac+opampcost

let cdeviation=abs(1000-Cfreq)
let gaindeviation=abs(100-gainfreq)
let merit = 1/(cost*(gaindeviation+cdeviation+1e-6))


let Zin=abs(v(in)[20]/vin#branch[20])

*input impedance in ohm
echo "Zin_TAB"
print Zin
echo "Zin_END"




echo "merit_TAB"
echo "Gain(dB)=$&gaindb"
echo "Gain= $&gainfreq"
echo "Central Frequency(Hz)= $&Cfreq"
echo "Gain deviation=$&gaindeviation"
echo "Central frequency deviation(Hz)=$&cdeviation"
echo "Cost(MU)= $&cost"
echo "Merit = $&merit"
echo "merit_END"


quit
.endc 
.end
